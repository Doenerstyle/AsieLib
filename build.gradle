buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

file "build.properties" withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = "${config.asielib.version}"
group= "pl.asie.lib" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "AsieLib"

minecraft {
    version = "1.7.10-10.13.0.1208"
    runDir = "eclipse/assets"
}

dependencies {
    compile "com.gregoriust.gregtech:gregtech_1.7.10:5.05.06:dev"
}

processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output.classesDir
    classifier = 'deobf'
}

task makeJars << {
    description = 'Creates the mod files'
}
makeJars.dependsOn clean, build, deobfJar
build.shouldRunAfter clean
deobfJar.shouldRunAfter build

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact deobfJar
        }
    }
    repositories {
        maven {
            url "${config.maven.url}"
        }
    }
}
